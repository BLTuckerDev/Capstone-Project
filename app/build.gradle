apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'org.sonarqube'

repositories {
    maven {
        url 'https://maven.google.com'
        // Alternative URL is 'https://dl.google.com/dl/android/maven2/'
    }
    mavenCentral()
}

android {
    signingConfigs {
        releaseConfig {
            keyAlias 'key'
            keyPassword 'password'
            storeFile file('./keystore/keystore.jks')
            storePassword 'password'
        }
    }
    compileSdkVersion 26
    buildToolsVersion "26.0.0"
    defaultConfig {
        applicationId "dev.bltucker.nanodegreecapstone"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        minSdkVersion 21
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.releaseConfig
        }
    }

    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }

    lintOptions {
        warning 'InvalidPackage'
    }

    dataBinding {
        enabled = true
    }
}


sonarqube {
    properties {
        // required metadata
        property "sonar.projectKey", "Nanodegree-Capstone"
        property "sonar.projectName", "Nanodegree-Capstone"
        property "sonar.projectVersion", "${version}"

        // path to source directories (required)
        property "sonar.sources", "src"
        property "sonar.test", "src/androidTest/java, src/test"
        property "sonar.exclusions", "build/**/*.*, src/androidTest/**/*.java, src/test/**/*.java"

        // other configs
        property "sonar.java.source", "7"

        // Android Lint
        property "sonar.android.lint.report", "build/outputs/lint-results.xml"
    }
}

final DAGGER_VERSION = "2.10"
final SUPPORT_VERSION = "26.0.0"
final SCHEMATIC_VERSION = "0.7.0"
final GOOGLE_PLAY_SERVICES_VERSION = "11.0.4"
final JSR_250_VERSION = "1.0"
final TIMBER_VERSION = "4.5.1"
final GSON_VERSION = "2.7"
final RETROFIT_VERSION = "2.1.0"
final BUTTER_KNIFE_VERSION = "8.7.0"
final RX_JAVA_VERSION = "1.2.3"
final RX_ANDROID_VERSION = "1.2.1"
final JUNIT_VERSION = "4.12"
final MOCKITO_CORE_VERSION = "2.8.9"
final POWERMOCK_API_MOCKITO_2_VERSION = "1.7.0RC4"
final POWERMOCK_JUNIT_MODULE_VERSION = "1.7.0RC4"
final GOOGLE_DEXMAKER_MOCKITO_VERSION = "1.2"
final TEST_RUNNER_VERSION = "0.5"
final TEST_RULES_VERSION = "0.5"
final HAMCREST_LIBRARY_VERSION = "1.3"
final ESPRESSO_VERSION = "2.2.2"
final UI_AUTOMATOR_VERSION = "2.1.2"
final ROOM_VERSION = "1.0.0-alpha8"

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    annotationProcessor "android.arch.persistence.room:compiler:${ROOM_VERSION}"
    implementation "android.arch.persistence.room:runtime:${ROOM_VERSION}"
    implementation "android.arch.persistence.room:rxjava2:${ROOM_VERSION}"

    annotationProcessor "com.google.dagger:dagger-compiler:${DAGGER_VERSION}"
    implementation "com.google.dagger:dagger:${DAGGER_VERSION}"

    compileOnly "javax.annotation:jsr250-api:${JSR_250_VERSION}"

    annotationProcessor "net.simonvt.schematic:schematic-compiler:${SCHEMATIC_VERSION}"
    implementation "net.simonvt.schematic:schematic:${SCHEMATIC_VERSION}"


    implementation "com.android.support:appcompat-v7:${SUPPORT_VERSION}"
    implementation "com.android.support:design:${SUPPORT_VERSION}"
    implementation "com.android.support:recyclerview-v7:${SUPPORT_VERSION}"
    implementation "com.android.support:support-annotations:${SUPPORT_VERSION}"
    implementation "com.android.support:cardview-v7:${SUPPORT_VERSION}"
    implementation "com.android.support:support-v4:${SUPPORT_VERSION}"
    implementation "com.android.support:customtabs:${SUPPORT_VERSION}"

    implementation "com.google.android.gms:play-services-analytics:${GOOGLE_PLAY_SERVICES_VERSION}"
    implementation "com.google.android.gms:play-services-location:${GOOGLE_PLAY_SERVICES_VERSION}"

    implementation "com.jakewharton.timber:timber:${TIMBER_VERSION}"
    implementation "com.google.code.gson:gson:${GSON_VERSION}"

    implementation "com.squareup.retrofit2:retrofit:${RETROFIT_VERSION}"
    implementation "com.squareup.retrofit2:adapter-rxjava:${RETROFIT_VERSION}"
    implementation "com.squareup.retrofit2:converter-gson:${RETROFIT_VERSION}"

    annotationProcessor "com.jakewharton:butterknife-compiler:${BUTTER_KNIFE_VERSION}"
    implementation "com.jakewharton:butterknife:${BUTTER_KNIFE_VERSION}"

    implementation "io.reactivex:rxandroid:${RX_ANDROID_VERSION}"
    implementation "io.reactivex:rxjava:${RX_JAVA_VERSION}"

    implementation "com.google.firebase:firebase-core:${GOOGLE_PLAY_SERVICES_VERSION}"
    implementation "com.google.firebase:firebase-crash:${GOOGLE_PLAY_SERVICES_VERSION}"

    testImplementation "junit:junit:${JUNIT_VERSION}"

    testImplementation ("org.mockito:mockito-core:${MOCKITO_CORE_VERSION}"){
        exclude module: "hamcrest-core"
    }

    testImplementation ("org.powermock:powermock-api-mockito2:${POWERMOCK_API_MOCKITO_2_VERSION}"){
        exclude module: "hamcrest-core"
        exclude module: "objenesis"
    }


    testImplementation ("org.powermock:powermock-module-junit4:${POWERMOCK_JUNIT_MODULE_VERSION}"){
        exclude module: "hamcrest-core"
        exclude module: "objenesis"
    }

    androidTestImplementation "com.google.dexmaker:dexmaker-mockito:${GOOGLE_DEXMAKER_MOCKITO_VERSION}"
    androidTestImplementation "com.google.dexmaker:dexmaker:${GOOGLE_DEXMAKER_MOCKITO_VERSION}"

    androidTestImplementation ("com.android.support.test:runner:${TEST_RUNNER_VERSION}"){
        exclude module: "support-annotations"
    }
    androidTestImplementation ("com.android.support.test:rules:${TEST_RULES_VERSION}"){
        exclude module: "support-annotations"
    }
    // Optional -- Hamcrest library
    androidTestImplementation "org.hamcrest:hamcrest-library:${HAMCREST_LIBRARY_VERSION}"
    // Optional -- UI testing with Espresso
    androidTestImplementation ("com.android.support.test.espresso:espresso-core:${ESPRESSO_VERSION}"){
        exclude module: "support-annotations"
    }
    // Optional -- UI testing with UI Automator
    androidTestImplementation ("com.android.support.test.uiautomator:uiautomator-v18:${UI_AUTOMATOR_VERSION}"){
        exclude module: "support-annotations"
    }

    androidTestImplementation ("com.android.support.test.espresso:espresso-contrib:${ESPRESSO_VERSION}"){
        exclude module: "support-annotations"
        exclude module: "support-v4"
        exclude module: "design"
        exclude module: "appcompat-v7"
        exclude module: "recyclerview-v7"
    }
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

}


apply plugin: "com.google.gms.google-services"
