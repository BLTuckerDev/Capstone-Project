apply plugin: 'com.android.application'
apply plugin: 'org.sonarqube'

android {
    signingConfigs {
        releaseConfig {
            keyAlias 'key'
            keyPassword 'password'
            storeFile file('./keystore/keystore.jks')
            storePassword 'password'
        }
    }
    compileSdkVersion 25
    buildToolsVersion "25.0.3"
    defaultConfig {
        applicationId "dev.bltucker.nanodegreecapstone"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        minSdkVersion 21
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.releaseConfig
        }
    }

    lintOptions {
        warning 'InvalidPackage'
    }

    dataBinding {
        enabled = true
    }
}


sonarqube {
    properties {
        // required metadata
        property "sonar.projectKey", "Nanodegree-Capstone"
        property "sonar.projectName", "Nanodegree-Capstone"
        property "sonar.projectVersion", "${version}"

        // path to source directories (required)
        property "sonar.sources", "src"
        property "sonar.test", "src/androidTest/java, src/test"
        property "sonar.exclusions", "build/**/*.*, src/androidTest/**/*.java, src/test/**/*.java"

        // other configs
        property "sonar.java.source", "7"

        // Android Lint
        property "sonar.android.lint.report", "build/outputs/lint-results.xml"
    }
}

final DAGGER_VERSION = "2.10"
final SUPPORT_VERSION = "25.3.1"
final SCHEMATIC_VERSION = "0.7.0"
final GOOGLE_PLAY_SERVICES_VERSION = "10.2.6"
final FIREBASE_VERSION = "10.2.6"
final JSR_250_VERSION = "1.0"
final TIMBER_VERSION = "4.1.1"
final GSON_VERSION = "2.7"
final RETROFIT_VERSION = "2.1.0"
final BUTTER_KNIFE_VERSION = "7.0.1"
final RX_JAVA_VERSION = "1.1.0"
final RX_ANDROID_VERSION = "1.1.0"
final JUNIT_VERSION = "4.12"
final MOCKITO_CORE_VERSION = "2.8.9"
final POWERMOCK_API_MOCKITO_2_VERSION = "1.7.0RC4"
final POWERMOCK_JUNIT_MODULE_VERSION = "1.7.0RC4"
final GOOGLE_DEXMAKER_MOCKITO_VERSION = "1.2"
final TEST_RUNNER_VERSION = "0.5"
final TEST_RULES_VERSION = "0.5"
final HAMCREST_LIBRARY_VERSION = "1.3"
final ESPRESSO_VERSION = "2.2.2"
final UI_AUTOMATOR_VERSION = "2.1.2"

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')

    annotationProcessor "com.google.dagger:dagger-compiler:${DAGGER_VERSION}"
    compile "com.google.dagger:dagger:${DAGGER_VERSION}"

    provided "javax.annotation:jsr250-api:${JSR_250_VERSION}"

    annotationProcessor "net.simonvt.schematic:schematic-compiler:${SCHEMATIC_VERSION}"
    compile "net.simonvt.schematic:schematic:${SCHEMATIC_VERSION}"


    compile "com.android.support:appcompat-v7:${SUPPORT_VERSION}"
    compile "com.android.support:design:${SUPPORT_VERSION}"
    compile "com.android.support:recyclerview-v7:${SUPPORT_VERSION}"
    compile "com.android.support:support-annotations:${SUPPORT_VERSION}"
    compile "com.android.support:cardview-v7:${SUPPORT_VERSION}"
    compile "com.android.support:support-v4:${SUPPORT_VERSION}"
    compile "com.android.support:customtabs:${SUPPORT_VERSION}"

    compile "com.google.android.gms:play-services-analytics:${GOOGLE_PLAY_SERVICES_VERSION}"
    compile "com.google.android.gms:play-services-location:${GOOGLE_PLAY_SERVICES_VERSION}"

    compile "com.jakewharton.timber:timber:${TIMBER_VERSION}"
    compile "com.google.code.gson:gson:${GSON_VERSION}"

    compile "com.squareup.retrofit2:retrofit:${RETROFIT_VERSION}"
    compile "com.squareup.retrofit2:adapter-rxjava:${RETROFIT_VERSION}"
    compile "com.squareup.retrofit2:converter-gson:${RETROFIT_VERSION}"
    compile "com.jakewharton:butterknife:${BUTTER_KNIFE_VERSION}"
    compile "io.reactivex:rxandroid:${RX_ANDROID_VERSION}"
    compile "io.reactivex:rxjava:${RX_JAVA_VERSION}"

    compile "com.google.firebase:firebase-core:${FIREBASE_VERSION}"
    compile "com.google.firebase:firebase-crash:${FIREBASE_VERSION}"

    testCompile "junit:junit:${JUNIT_VERSION}"

    testCompile ("org.mockito:mockito-core:${MOCKITO_CORE_VERSION}"){
        exclude module: "hamcrest-core"
    }

    testCompile ("org.powermock:powermock-api-mockito2:${POWERMOCK_API_MOCKITO_2_VERSION}"){
        exclude module: "hamcrest-core"
        exclude module: "objenesis"
    }


    testCompile ("org.powermock:powermock-module-junit4:${POWERMOCK_JUNIT_MODULE_VERSION}"){
        exclude module: "hamcrest-core"
        exclude module: "objenesis"
    }

    androidTestCompile "com.google.dexmaker:dexmaker-mockito:${GOOGLE_DEXMAKER_MOCKITO_VERSION}"
    androidTestCompile "com.google.dexmaker:dexmaker:${GOOGLE_DEXMAKER_MOCKITO_VERSION}"

    androidTestCompile ("com.android.support.test:runner:${TEST_RUNNER_VERSION}"){
        exclude module: "support-annotations"
    }
    androidTestCompile ("com.android.support.test:rules:${TEST_RULES_VERSION}"){
        exclude module: "support-annotations"
    }
    // Optional -- Hamcrest library
    androidTestCompile "org.hamcrest:hamcrest-library:${HAMCREST_LIBRARY_VERSION}"
    // Optional -- UI testing with Espresso
    androidTestCompile ("com.android.support.test.espresso:espresso-core:${ESPRESSO_VERSION}"){
        exclude module: "support-annotations"
    }
    // Optional -- UI testing with UI Automator
    androidTestCompile ("com.android.support.test.uiautomator:uiautomator-v18:${UI_AUTOMATOR_VERSION}"){
        exclude module: "support-annotations"
    }

    androidTestCompile ("com.android.support.test.espresso:espresso-contrib:${ESPRESSO_VERSION}"){
        exclude module: "support-annotations"
        exclude module: "support-v4"
        exclude module: "design"
        exclude module: "appcompat-v7"
        exclude module: "recyclerview-v7"
    }

}


apply plugin: "com.google.gms.google-services"
